using System;
using System.Drawing;
using System.Drawing.Imaging;
using System.IO;
using System.Xml;
using Nemerle.Collections;

using Nextem.String;

namespace IronMacro.Core {
	public type MapRect = int * int * int * int;
	
	public enum TriggerOn {
		| Appear
		| Disappear
		| Change
		| Both
	}
	
	public variant Spec {
		| Class {
				_Name : string;
				Members : Hashtable [string, MapRect * Spec]
			}
		| Trigger {
				_Name : string;
				On : TriggerOn;
				Image : string;
			}
		| EnumMapping {
				_Name : string;
				Members : Hashtable [string, int * string]
			}
		| StringMapping {
				_Name : string;
				Mapping : Hashtable [string, string]
			}
		| IntMapping {
				_Name : string;
				Mapping : Hashtable [string, string]
			}
		| BoolMapping {
				_Name : string;
				mutable True : string;
				mutable False : string
			}
		| Ref {
				_Name : string;
				_Spec : Spec
			}
		
		public Name : string {
			get {
				match(this) {
					| Class(name, _)
					| Trigger(name, _, _)
					| EnumMapping(name, _)
					| StringMapping(name, _)
					| IntMapping(name, _)
					| BoolMapping(name, _, _)
					| Ref(name, _) => 
						name
				}
			}
		}
		
		public override ToString() : string {
			mutable ret : string;
			match(this) {
				| EnumMapping(name, members) =>
					ret = "\t<Enum name=\"{0}\">\n" <- name;
					foreach(pair in members) {
						def (value, image) = pair.Value;
						ret += "\t\t<Elem name=\"{0}\" value=\"{1}\" {2}/>\n" <- (
								pair.Key, 
								value, 
								if(image == null) ""
								else "image=\"{0}\" " <- image
							)
					}
					ret + "\t</Enum>"
				| Class(name, members) =>
					ret = "\t<Class name=\"{0}\">\n" <- name;
					foreach((rect, spec) in members.Values) {
						def rect = 
							if(rect[0] == -1 && rect[1] == -1 && rect[2] == -1 && rect[3] == -1) ""
							else "rect=\"{0},{1},{2},{3}\" " <- rect;
						match(spec) {
							| Trigger(name, on, image) =>
								ret += "\t\t<Trigger name=\"{0}\" on=\"{1}\" {2}{3}/>\n" <- (
										name, 
										match(on) {
											| Appear => "appear"
											| Disappear => "disappear"
											| Both => "both"
											| Change => "change"
										}, 
										if(image == null) ""
										else "image=\"{0}\" " <- image, 
										rect
									)
							| Ref(name, spec) =>
								ret += "\t\t<Prop name=\"{0}\" type=\"{1}\" {2}/>\n" <- (name, spec.Name, rect)
							| StringMapping(_, mapping)
							| IntMapping(_, mapping)
							| BoolMapping(_, t, f)
								with mapping = Hashtable([("true", t), ("false", f)]) => 
								mutable subret = "";
								foreach(pair in mapping)
									subret += 
										if(pair.Value == null) ""
										else "\t\t\t<Value value=\"{0}\" name=\"{1}\" />\n" <- (pair.Key, pair.Value);
								
								ret += "\t\t<Prop name=\"{0}\" type=\"{1}\" {2}>\n{3}\t\t</Prop>\n" <- (
										name, 
										match(spec) {
											| StringMapping => "string"
											| IntMapping => "int"
											| BoolMapping => "bool"
											| _ => ""
										}, 
										rect, 
										subret
									)
							| _ => ()
						}
					}
					ret + "\t</Class>\n"
				| _ => ""
			}
		}
		
		public static Load(fn : string) : list [Spec] {
			def hash = Hashtable.[string, Spec]();
			mutable order = [];
			
			def fs = File.OpenRead(fn);
			def xmlDoc = XmlDocument();
			xmlDoc.Load(fs);
			
			def enumNodes = xmlDoc.GetElementsByTagName("Enum");
			foreach(node is XmlNode in enumNodes) {
				def attrs = node.Attributes;
				unless(attrs.Count == 1 && attrs[0].Name == "name") throw Exception("Enum takes one attribute, 'name'.");
				
				def enumName = attrs[0].Value;
				order ::= enumName;
				mutable i = 0;
				def members = Hashtable();
				hash[enumName] = Spec.EnumMapping(enumName, members);
				
				foreach(child is XmlNode in node) {
					unless(child.Name == "Elem") throw Exception("Enum can only contain Elem elements.");
					
					def attrs = child.Attributes;
					def name = match(attrs.GetNamedItem("name")) { | null => null | x => x.InnerText }
					def value = match(attrs.GetNamedItem("value")) { | null => 0 | x => int.Parse(x.InnerText) }
					def image = match(attrs.GetNamedItem("image")) { | null => null | x => x.InnerText }
					
					when(name == null) throw Exception("Enum elements require a name.");
					
					members[name] = 
						(
							if(value == 0) {
								i++;
								i - 1
							} else {
								i = value+1;
								value
							}, 
							image
						)
				}
			}
			
			def classNodes = xmlDoc.GetElementsByTagName("Class");
			foreach(node is XmlNode in classNodes) {
				def attrs = node.Attributes;
				unless(attrs.Count == 1 && attrs[0].Name == "name") throw Exception("Class takes one attribute, 'name'.");
				
				def className = attrs[0].Value;
				order ::= className;
				def members = Hashtable();
				hash[className] = Spec.Class(className, members);
				
				foreach(child is XmlNode in node) {
					def attrs = child.Attributes;
					def name = match(attrs.GetNamedItem("name")) { | null => null | x => x.InnerText }
					def typ = match(attrs.GetNamedItem("type")) { | null => null | x => x.InnerText }
					def on = match(attrs.GetNamedItem("on")) { | null => null | x => x.InnerText }
					def image = match(attrs.GetNamedItem("image")) { | null => null | x => x.InnerText }
					def rect = match(attrs.GetNamedItem("rect")) { | null => null | x => x.InnerText }
					
					def rect = 
						if(rect == null) (-1, -1, -1, -1)
						else {
							def (a, b, c, d) = rect.Split3(",");
							(int.Parse(a), int.Parse(b), int.Parse(c), int.Parse(d))
						}
					
					when(name == null) throw Exception("Class members require a name.");
					
					def member = 
						match(child.Name) {
							| "Prop" =>
								def values = LoadValues(child);
								
								match(typ) {
									| "string" => Spec.StringMapping(name, values)
									| "int" => 
										Spec.StringMapping(name, values)
									| "bool" => 
										Spec.BoolMapping(
												name,
												if(values.ContainsKey("true")) values["true"] else null, 
												if(values.ContainsKey("false")) values["false"] else null
											)
									| rname => Spec.Ref(name, hash[rname])
								}
							
							| "Trigger" =>
								Spec.Trigger(
										name, 
										match(on) {
											| "appear" => TriggerOn.Appear
											| "disappear" => TriggerOn.Disappear
											| "both" => TriggerOn.Both
											| "change" => TriggerOn.Change
											| x => throw Exception("Unknown 'on' value to Trigger: {0}" <- x)
										}, 
										image
									)
							| x => throw Exception("Unknown member of Class: {0}" <- x)
						}
					members[name] = (rect, member)
				}
			}
			
			mutable ret = [];
			foreach(name in order)
				ret ::= hash[name];
			ret
		}
		
		static LoadValues(node : XmlNode) : Hashtable [string, string] {
			def values = Hashtable();
			
			foreach(child is XmlNode in node) {
				unless(child.Name == "Value") throw Exception("Class.Prop nodes can only include Value nodes.");
				
				def value = child.Attributes.GetNamedItem("value");
				def image = child.Attributes.GetNamedItem("image");
				unless(value != null && image != null) throw Exception("Class.Prop nodes must include value and image.");
				
				values[value.InnerText] = image.InnerText
			}
			
			values
		}
	}
}
